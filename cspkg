#!/usr/bin/env cs
import imgui_ext
import imgui
struct package_info
    var name=null
    var type=null
end
function get_package_info(path)
    var info=gcnew package_info
    var pos=path.find(".",0)
    if pos==-1
        throw runtime.exception("Unrecongnized File.")
    end
    info->name=path.substr(0,pos)
    info->type=path.substr(pos+1,path.size()-pos-1)
    return info
end
function get_file_name(path)
    var pos=path.rfind(to_string(system.path.separator),-1)
    if pos!=-1
        return path.substr(pos+1,path.size()-pos-1)
    else
        return path
    end
end
var path=runtime.get_import_path().split({system.path.delimiter}).back()
function menu()
    menu_item("Cspkg","",false)
    if begin_menu("Tools",true)
        if begin_menu("Styles",true)
            if menu_item("Classic","",true)
                style_color_classic()
            end
            if menu_item("Light","",true)
                style_color_light()
            end
            if menu_item("Dark","",true)
                style_color_dark()
            end
            end_menu()
        end
        end_menu()
    end
    if begin_menu("Help",true)
        if menu_item("About this software","",true)
            show_about=true
        end
        end_menu()
    end
    if menu_item("Exit","ESC",true)
        show_confirm=true
    end
end
function confirm()
    var confirm_window=true
    begin_window("Confirm",confirm_window,{flags.always_auto_resize,flags.no_collapse})
        set_window_focus()
        text("Do you want to exit?")
        spacing()
        if button("Yes") || is_key_pressed(to_integer('Y'))
            system.exit(0)
        end
        same_line()
        if button("No") || !confirm_window || is_key_pressed(to_integer('N'))
            show_confirm=false
        end
    end_window()
end
function about()
    var about_opened=true
    begin_window("About",about_opened,{flags.always_auto_resize,flags.no_collapse})
        set_window_focus()
        if !about_opened
            show_about=false
        end
        text("CovScript Package Manager")
        bullet()
        text("Version 1.0.0")
        if button("Ok")
            show_about=false
        end
    end_window()
end
struct inst_confirm_win extends imgui_ext.message_box
    function on_confirm() override
        status="Installation succeed."
    end
end
var install_confirm=new inst_confirm_win
install_confirm.title="Install"
install_confirm.message="Target exists.Overwrite?"
struct inst_win extends imgui_ext.file_explorer
    function on_confirm() override
        var file_name=get_file_name(this.path)
        if system.file.exists(path+system.path.separator+file_name)
            install_confirm.show()
            return
        end
        system.file.copy(this.path,path+system.path.separator+file_name)
        status="Installation succeed."
    end
end
var install=new inst_win
install.title="Install"
install.filters.push_back(".*\\.cse")
install.filters.push_back(".*\\.csp")
install.read_path()
function uninstall()
    var uninstall_window=true
    begin_window("Uninstall",uninstall_window,{flags.always_auto_resize,flags.no_collapse})
        set_window_focus()
        var info=packages.at(select)
        if info->type=="cse"
            text("Do you want to uninstall the extension \""+info->name+"\"?")
        else
            text("Do you want to uninstall the package \""+info->name+"\"?")
        end
        spacing()
        if button("Yes") || is_key_pressed(to_integer('Y'))
            var extension_name=path+system.path.separator+info->name+".cse"
            var package_name=path+system.path.separator+info->name+".csp"
            if !system.file.remove(extension_name)&&!system.file.remove(package_name)
                status="Uninstall failed.("+info->name+")"
            else
                status="Uninstall succeed.("+info->name+")"
            end
            load()
            show_uninstall=false
        end
        same_line()
        if button("No") || !uninstall_window || is_key_pressed(to_integer('N'))
            show_uninstall=false
        end
    end_window()
end
function gui()
    using imgui
    var show_about=false
    var show_confirm=false
    var show_install=false
    var show_uninstall=false
    var app=window_application(0.75*imgui.get_monitor_width(0),0.75*imgui.get_monitor_height(0),"CovScript Package Manager")
    var window_opened=true
    var window_flags={flags.no_saved_settings,flags.no_title_bar,flags.no_resize,flags.no_move,flags.no_collapse,flags.horizontal_scroll_bar}
    var select=0
    var status="No error."
    var items=new array
    var title=new string
    var packages=new array
    function load()
        items=new array
        packages=new array
        var info=system.path.scan(path)
        for it iterate info
            if it.type()==system.path.type.reg
                packages.push_back(get_package_info(it.name()))
            end
        end
        var max_size=0
        for it iterate packages
            if it->name.size()>max_size
                max_size=it->name.size()
            end
        end
        title="Name"
        for i=0 to max_size
            title+=" "
        end
        title+="Type"
        var item=new string
        for it iterate packages
            item=it->name
            for i=0 to max_size-it->name.size()
                item+=" "
            end
            item+="    "
            if it->type=="cse"
                item+="CovScript Extension"
            else
                item+="CovScript Package"
            end
            items.push_back(item)
        end
    end
    load()
    while !app.is_closed()
        app.prepare()
        if begin_main_menu_bar()
            menu()
            end_main_menu_bar()
        end
        if is_key_pressed(get_key_index(keys.escape))
            show_confirm=true
        end
        begin_window("cspkg_gui",window_opened,window_flags)
            set_window_pos(vec2(0,20))
            set_window_size(vec2(app.get_window_width(),app.get_window_height()-20))
            if begin_popup_window()
                menu()
                end_popup()
            end
            text(title)
            separator()
            list_box("Package List##list",select,items)
            if begin_popup_item("list")
                menu_item("Menu","",false)
                if menu_item("Refresh","",true)
                    load()
                end
                if begin_menu("Property",true)
                    var info=packages.at(select)
                    if info->type=="cse"
                        menu_item("CovScript Extension","",false)
                    else
                        menu_item("CovScript Package","",false)
                    end
                    menu_item("Name: "+info->name,"",false)
                    end_menu()
                end
                if menu_item("Uninstall","",true)
                    show_uninstall=true
                end
                end_popup()
            end
            text(status)
            if button("Refresh List")
                load()
            end
            if button("Install New")
                install.win_opened=true
            end
            if button("Uninstall")
                show_uninstall=true
            end
        end_window()
        if show_confirm
            confirm()
        end
        if show_about
            about()
        end
        if install_confirm.is_opened()
            install_confirm.render()
        end
        if install.is_opened()
            install.render()
        end
        if show_uninstall
            uninstall()
        end
        app.render()
    end
end
function answer(question)
    loop
        system.out.println(question+"(y/n)")
        switch system.console.getch().tolower()
            case 'y'
                return true
            end
            case 'n'
                return false
            end
            default
                system.out.println("Please press \'y\' for yes or \'n\' for not.")
            end
        end
    end
end
function draw_separator()
    for i=1 to system.console.terminal_width()
        system.out.print("-")
    end
    system.out.println("")
end
# Main Program
if system.args.size()<2
    system.out.println("Error: No command.")
    system.out.println("Enter \"--help\" to view help.")
    system.exit(-1)
end
switch system.args.at(1)
    default
        system.out.println("Unsupported Function.")
        system.exit(-1)
    end
    case "--install"
        if system.args.size()!=3
            system.out.println("Wrong syntax.")
            system.exit(-1)
        end
        var file_name=get_file_name(clone(system.args.at(2)))
        if system.file.exists(path+system.path.separator+file_name)
            if !answer("Target exists.Overwrite?")
                system.out.println("Installation aborted.")
                system.exit(0)
            end
        end
        system.file.copy(system.args.at(2),path+system.path.separator+file_name)
        system.out.println("Installation succeed.")
    end
    case "--uninstall"
        if system.args.size()!=3
            system.out.println("Wrong syntax.")
            system.exit(-1)
        end
        var extension_name=path+system.path.separator+system.args.at(2)+".cse"
        var package_name=path+system.path.separator+system.args.at(2)+".csp"
        if !system.file.remove(extension_name)&&!system.file.remove(package_name)
            system.out.println("Uninstall failed.")
            system.exit(-1)
        else
            system.out.println("Uninstall succeed.")
            system.exit(0)
        end
    end
    case "--list"
        system.out.println("Installed Package/Extension")
        draw_separator()
        var info=system.path.scan(path)
        var packages=new array
        for it iterate info
            if it.type()==system.path.type.reg
                packages.push_back(get_package_info(it.name()))
            end
        end
        var max_size=0
        for it iterate packages
            if it->name.size()>max_size
                max_size=it->name.size()
            end
        end
        system.out.print("Name")
        for i=0 to max_size
            system.out.print(" ")
        end
        system.out.println("Type")
        draw_separator()
        for it iterate packages
            system.out.print(it->name)
            for i=0 to max_size-it->name.size()
                system.out.print(" ")
            end
            system.out.print("    ")
            if it->type=="cse"
                system.out.println("CovScript Extension")
            else
                system.out.println("CovScript Package")
            end
        end
    end
    case "--gui"
        gui()
    end
    case "--help"
@begin
system.out.print("
Covariant Script Package Manager 1.0.0\n
    --install   <path>    Install a CovScript Package/Extension\n
    --uninstall <name>    Uninstall a CovScript Package/Extension\n
    --list                List Installed CovScript Package/Extension\n
    --gui                 Run Cspkg GUI.\n
    --help                Print help infomation.\n
    --version             Print version info.\n
")
@end
    end
    case "--version"
        system.out.println("cspkg v1.0.0")
    end
end